#!/usr/bin/perl -w

package dod_daemon;

use strict;
use Getopt::Long;
use Proc::Daemon;

# Initializes Logging Settings

use DOD::Config qw($config);
use DOD;

our ($logger, $workdir, $pidfile); 

INIT{
    $logger = Log::Log4perl::get_logger("DOD.Daemon");
    $logger->debug("Logger created");
    $workdir = $config->{'DAEMON_WORKDIR'};
    $pidfile = "$workdir/$config->{'DAEMON_PIDFILE'}";
}

sub usage(){
    print "USAGE:\n\tcommand --start\n\tcommand --stop\n";
    exit -1;
}

sub Main{

    my ($start, $stop);
    usage() if (@ARGV != 1 or ! GetOptions('start' => \$start, 'stop' => \$stop));

    my $daemon = Proc::Daemon->new(work_dir => $workdir,
                                    child_STDOUT => ">>$config->{'DAEMON_STDOUT'}",
                                    child_STDERR => ">>$config->{'DAEMON_STDERR'}",
                                    pid_file => $pidfile);

    $logger->debug( "Workdir: $workdir, pidfile: $pidfile" );

    if (-e $pidfile){
        my $pid = `cat $pidfile`;
        if ($stop){
            $logger->debug( "Stopping daemon process PID: $pid" );
            print "Stopping daemon process PID: $pid\n" ;
            my $stopped = $daemon->Kill_Daemon( $pid );
            $logger->debug( "Number of processes killed: $stopped ");
            unlink($pidfile);
            exit 0;
        }
        elsif ($start){
            chomp $pid;
            print "A daemon is running with PID: $pid\n";
            exit -1;
        }
    }
    else{
        if ($start){
            $logger->debug( "Starting DOD daemon" );
            my $dispatcher = $daemon->Init();
            unless ($dispatcher){
                # Daemon process
                eval{
                    DOD::jobDispatcher();
                    1;
                } or do {
                    $logger->error( "The daemon process died!" );
                    unlink($pidfile);
                    exit -1;
                }
            }
            $logger->debug( "Job dispatcher PID: $dispatcher" );
            my $status = $daemon->Status($dispatcher);
            print "PID: $dispatcher\n";
            exit 0;
        }
        elsif($stop){
            print("No processes running.\n");
            exit -1;
        }
    }
}

&Main();
1;

END{}

=head1 NAME

dod_daemon - Launch DB on Demand daemon

=cut

=head1 USE

=over

=item * 

dod_daemon --start


=item *

dod_daemon --stop

=back

=cut

=head1 COMMENTS

This is the launching script for the DB on Demand job dispatching daemon.

Configuration and additional support files (i.e. templates for DBMS configuration files)
can be found at: <PATH_TO_PERL_SHARE>/DOD/. Expected files are:

- dod.conf : General configuration file. YAML format

- dod_daemon_logger.conf : Logger configuration file

- templates/MY_CNF : MySQL parameter template. YAML format

=head1 AUTHOR

Ignacio Coterillo <ignacio.coterillo[at]cern[dot]ch> 

IT-DB-DBB


=cut
